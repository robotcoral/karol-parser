@top Program { statement* }

@skip { space | comment }

IdentifierWithParam { 
  Identifier "(" (Colour | Number) ")" 
}

Subroutine { 
  kw<"Anweisung"> Identifier 
    statement* 
  (kw<"endeAnweisung"> | kwa<"*Anweisung">) 
}

Condition { 
  kw<"Bedingung"> Identifier 
    statement* 
  (kw<"endeBedingung"> | kwa<"*Bedingung">) 
}

If { 
  kw<"wenn"> kw<"nicht">? (Identifier | IdentifierWithParam) kw<"dann"> 
    statement* 
  (kw<"sonst"> 
    statement*)? 
  (kw<"endewenn"> | kwa<"*wenn">)
}

While { 
  kw<"wiederhole"> (kw<"immer"> | Number kw<"mal"> | kw<"solange"> kw<"nicht">? (Identifier | IdentifierWithParam)) 
    statement* 
  (kw<"endewiederhole"> | kwa<"*wiederhole">) 
} 

WhileEnd {
  kw<"wiederhole"> 
    statement* 
  (kw<"endewiederhole"> | kwa<"*wiederhole">) ((kw<"solange"> | kw<"bis">) kw<"nicht">? (Identifier | IdentifierWithParam)) 
}

statement {
  Identifier |
  IdentifierWithParam |
  Subroutine |
  Condition |
  If |
  While |
  WhileEnd
}

@tokens {
  Identifier { $[a-zA-Z_äüö] $[a-zA-Z_äüö\-0-9]* }
  Number { $[0-9]+ }
  Colour { "rot" | "gelb" | "blau" | "grün" | "schwarz" }
  space { $[ \t\n\r;]+ }
  comment { "{" $[a-zA-Z_äüö \t\n\r;\-0-9]* "}" }
  
  "(" ")"

  @precedence {Colour, Number, Identifier}
}

kw<word> { @specialize[@name=Keyword]<Identifier, word> }
kwa<word> { [@name=Keyword] {word} }

@detectDelim
